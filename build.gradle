buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.2'
    id 'io.freefair.lombok' version '3.2.0'
}

group 'org.grohe'
version '0.0.8'

compileJava {
    sourceCompatibility = '1.8'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['GroheOndusApi']
    pkg {
        repo = 'java-libraries'
        name = 'grohe-ondus-api'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/floriansw/grohe-ondus-api-java.git'
        version {
            name = '0.0.8'
            desc = 'GROHE ONDUS Api library'
            released  = new Date()
            vcsTag = '0.0.8'
        }
    }
}

publishing {
    publications {
        GroheOndusApi(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.5.5'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.mockito:mockito-core:2.+"
}
