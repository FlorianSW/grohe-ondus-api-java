buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:6.4.0'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'signing'
    id 'io.freefair.lombok' version '8.3'
}

apply plugin: 'biz.aQute.bnd.builder'
group 'io.github.floriansw'
version '3.0.0'
archivesBaseName = 'ondus-api'

compileJava {
    sourceCompatibility = '1.8'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'grohe-ondus-api-java'
                description = 'A library which can communicate to the Grohe ONDUS API interface, e.g. to request appliance data or to send commands.'
                url = 'https://github.com/FlorianSW/grohe-ondus-api-java'
                licenses {
                    license {
                        name = 'MIT'
                        url = '"https://github.com/FlorianSW/grohe-ondus-api-java/blob/$version/LICENSE"'
                    }
                }
                developers {
                    developer {
                        id = 'FlorianSW'
                        name = 'Florian Schmidt'
                        email = 'florian.schmidt.welzow@t-online.de'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/FlorianSW/grohe-ondus-api-java'
                    developerConnection = 'scm:git:https://github.com/FlorianSW/grohe-ondus-api-java'
                    url = 'https://github.com/FlorianSW/grohe-ondus-api-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "ossrh"
            credentials(PasswordCredentials)
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation "org.mockito:mockito-core:2.28.2"
}
